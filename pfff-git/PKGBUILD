# Maintainer: Francisco Lopes <chico.lopes@gmail.com>

pkgname='pfff-git'
pkgver=v0.27.3.r118.g7382b4f
pkgrel=1
pkgdesc='pfff is a set of tools and APIs to perform some static analysis, dynamic analysis, code visualizations, code navigations, or style-preserving source-to-source transformations'
arch=('x86_64')
url='https://github.com/facebook/pfff'
license=('LGPL2.1')
optdepends=('gtk2: required by codegraph and codemap')
makedepends=('git' 'ocaml')
source=('git+https://github.com/facebook/pfff.git')
md5sums=('SKIP')
conflicts=('pfff')
provides=('pfff')

_gitname=pfff

pkgver() {
    cd $srcdir/$_gitname
    git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd $srcdir/$_gitname
    ./configure --prefix=/usr
    make depend
    make
}

package() {
    cd $srcdir/$_gitname
    make DESTDIR=$pkgdir install
}
